
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗаполнитьПоляЗадач(Объект);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьПоляЗадач(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьПоляЗадач(ТекущийОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти


#Область ОбработчикиКомандФормы
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПараметрыПоляСоответствуютПараметрамПоУмолчанию(СтрокаПоля)
	ПоУмолчанию = УправлениеЗадачами.ЗначенияПараметровПроверкиПоляПоУмолчанию();

	Для Каждого КлючЗначение Из ПоУмолчанию Цикл
		Если КлючЗначение.Значение<>СтрокаПоля[КлючЗначение.Ключ] Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;

	Возврат Истина;
КонецФункции

&НаСервере
Процедура ЗаполнитьПоляЗадач(ТекущийОбъект, Очищать = Истина)
	Если Очищать Тогда
		ПоляЗадачи.Очистить();
	КонецЕсли;
	
	СтруктураРеквизитовЗадачи = УправлениеЗадачами.СтруктураРеквизитовЗадачБезАвтоматическиИзменяемых();
	
	ЗначенияПоУмолчанию = УправлениеЗадачами.ЗначенияПараметровПроверкиПоляПоУмолчанию();
	
	Для Каждого КлючЗначение ИЗ СтруктураРеквизитовЗадачи Цикл
		Если КлючЗначение.Ключ = "Тема" Тогда
			Продолжить;
		КонецЕсли;
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ИмяРеквизита",КлючЗначение.Ключ);
		
		НайденныеСтроки  = ПоляЗадачи.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество()=0 Тогда
			НоваяСтрока = ПоляЗадачи.Добавить();
			НоваяСтрока.ИмяРеквизита = КлючЗначение.Ключ;
			НоваяСтрока.Представление = КлючЗначение.Значение;
		Иначе
			НоваяСтрока = НайденныеСтроки[0];
		КонецЕсли;
		
		СтрокиОбъекта = ТекущийОбъект.НастройкаПолейЗадачи.НайтиСтроки(СтруктураПоиска);
		Если СтрокиОбъекта.Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначенияПоУмолчанию);
		Иначе
			НоваяСтрока.Видимость = СтрокиОбъекта[0].Видимость;
			НоваяСтрока.Доступность = СтрокиОбъекта[0].Доступность;
			НоваяСтрока.Обязательность = СтрокиОбъекта[0].Обязательность;
		КонецЕсли;
		
	КонецЦикла;
	
	ПоляЗадачи.Сортировать("Представление");
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПоляЗадач(ТекущийОбъект)
	ТекущийОбъект.НастройкаПолейЗадачи.Очистить();
	
	Для Каждого СтрокаПоля Из ПоляЗадачи Цикл
		Если ПараметрыПоляСоответствуютПараметрамПоУмолчанию(СтрокаПоля) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТекущийОбъект.НастройкаПолейЗадачи.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПоля);
	КонецЦикла;	
КонецПроцедуры

#КонецОбласти