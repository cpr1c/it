
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задача") Тогда
		// Заполнение шапки
		Предмет = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр Трудозатраты 
	Движения.Трудозатраты.Записывать = Истина;
	
	Движение = Движения.Трудозатраты.Добавить();
	Движение.Период = Дата;
	Движение.Предмет = Предмет;
	Движение.Автор = Автор;
	Движение.ВидДеятельности = ВидДеятельности;
	Движение.Факт = Часов;
	Движение.Содержание = Комментарий;
	
	// регистр Обучение 
	Движения.Обучение.Записывать = Истина;
	Движения.Обучение.Очистить();
	
	Если ТипЗнч(Предмет) = Тип("ДокументСсылка.Задача") Тогда
		
		ОснованиеЗадачи = Предмет.Основание;
		Если ТипЗнч(ОснованиеЗадачи) = Тип("СправочникСсылка.Уроки") Тогда
			
			ПланОбучения = Обучение.ПланОбученияПоЗадаче(Предмет);
			
			Если ЗначениеЗаполнено(ПланОбучения) Тогда
				
				Движение = Движения.Обучение.Добавить();
				Движение.Период = Дата;
				Движение.Пользователь = Автор;
				Движение.Курс = ПланОбучения.Курс;
				Движение.Урок = ОснованиеЗадачи;
				Движение.Дата = Дата;
				Движение.Факт = Часов;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если НачалоДня(Дата)=НачалоДня(ТекущаяДатаСеанса()) Тогда
		Дата=ТекущаяДатаСеанса();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = '00010101000000';
	ДатаИзменения = '00010101000000';
	Автор = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	Редактор = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
	
КонецПроцедуры
