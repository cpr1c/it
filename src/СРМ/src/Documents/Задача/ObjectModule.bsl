
#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	РеквизитыДоИзменения = Новый Структура("Статус,ДатаВыполнения,ДатаЗакрытия,ДатаНачалаРаботПоЗадаче");
	ЗаполнитьЗначенияСвойств(РеквизитыДоИзменения, Ссылка);
	
	СтатусНовый = УправлениеЗадачамиПовтИсп.СтатусЗадачиНовый();
	СтатусВРаботе = УправлениеЗадачамиПовтИсп.СтатусЗадачиВРаботе();
	СтатусыНеВыполненных = УправлениеЗадачамиПовтИсп.СтатусыНеВыполненныхЗадач();
	СтатусыВыполненных = УправлениеЗадачамиПовтИсп.СтатусыСписка(Справочники.СпискиСтатусовЗадач.Выполненные);
	СтатусыЗакрытых = УправлениеЗадачамиПовтИсп.СтатусыСписка(Справочники.СпискиСтатусовЗадач.Закрытые);
	
	// ДатаВыполнения
	Если СтатусыНеВыполненных.НайтиПоЗначению(РеквизитыДоИзменения.Статус) <> Неопределено 
		И СтатусыНеВыполненных.НайтиПоЗначению(Статус) = Неопределено Тогда
		
		ДатаВыполнения = ТекущаяДатаСеанса();
	ИначеЕсли СтатусыНеВыполненных.НайтиПоЗначению(Статус) <> Неопределено Тогда
		ДатаВыполнения = '00010101000000';
	КонецЕсли;
	
	// ДатаЗакрытия
	Если СтатусыЗакрытых.НайтиПоЗначению(РеквизитыДоИзменения.Статус) = Неопределено 
		И СтатусыЗакрытых.НайтиПоЗначению(Статус) <> Неопределено Тогда
		
		ДатаЗакрытия = ТекущаяДатаСеанса();
	ИначеЕсли СтатусыЗакрытых.НайтиПоЗначению(Статус) = Неопределено Тогда
		ДатаЗакрытия = '00010101000000';
	КонецЕсли;
	
	// ДатаНачалаРаботПоЗадаче
	Если (Не ЗначениеЗаполнено(РеквизитыДоИзменения.Статус) ИЛИ РеквизитыДоИзменения.Статус = СтатусНовый) 
		И Статус <> СтатусНовый Тогда
		
		ДатаНачалаРаботПоЗадаче = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Установка срока исполнения авто, если срок задан вручную
	Если ЗначениеЗаполнено(СрокИсполнения) Тогда
		СрокИсполненияАвто = СрокИсполнения;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("Изменения", УправлениеЗадачами.ИзмененияОбъектаПередЗаписью(ЭтотОбъект, Ложь));
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ТрудозатратыПоЗадачам 
	Движения.Трудозатраты.Записывать = Истина;
	Движение = Движения.Трудозатраты.Добавить();
	Движение.Период = Дата;
	Движение.Предмет = Ссылка;
	Движение.Автор = Исполнитель;
	Движение.План = ОценкаТрудозатратИсполнителя;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Статус = УправлениеЗадачамиПовтИсп.СтатусЗадачиНовый();
	СодержаниеФормат=УправлениеЗадачамиПовтИсп.ФорматТекстаКомментарияПоУмолчанию();
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Задача") Тогда
		
		РодительскаяЗадача = ДанныеЗаполнения;
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РодительскаяЗадача, "КонтактОбращения,Проект,Спринт,СрокИсполнения,Срочность");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Статус = УправлениеЗадачамиПовтИсп.СтатусЗадачиНовый();
	
	ДатаСоздания = '00010101000000';
	ДатаИзменения = '00010101000000';
	ДатаЗакрытия = '00010101000000';
	ДатаВыполнения = '00010101000000';
	ДатаНачалаРаботПоЗадаче = '00010101000000';
	СрокИсполненияАвто = '00010101000000';
	
	Если ТипЗнч(Основание) = Тип("СправочникСсылка.Уроки") Тогда
		Основание = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РедакторКомментария.ПриЗаписиОбъекта(ЭтотОбъект, Ссылка);
	УправлениеЗадачами.ЗаписатьИзмененияКлючевыхРеквизитовЗадачиПриЗаписи(ЭтотОбъект);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПараметрыПроверкиДляСтатуса = УправлениеЗадачами.НастройкаПроверкиПолейДляСтатуса(Статус);
	
	Для Каждого КлючЗначение Из ПараметрыПроверкиДляСтатуса Цикл
		Если КлючЗначение.Значение.Обязательность Тогда
			ПроверяемыеРеквизиты.Добавить(КлючЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;

	Если ОценкаТрудозатрат < ДополнительныеИсполнители.Итог("ОценкаТрудозатрат") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Оценка трудозатрат не может быть меньше итоговой оценки по дополнительным исполнителям",
														  ,
														  ,
														  ,
														  Отказ);
	КонецЕсли;
КонецПроцедуры



#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//  Код процедур и функций
#КонецОбласти

#Область Инициализация

#КонецОбласти


