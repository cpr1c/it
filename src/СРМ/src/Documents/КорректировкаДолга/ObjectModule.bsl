

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ВзаиморасчетыСКонтрагентами.Очистить();
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	
	Если ВидОперации=Перечисления.ВидыОперацийКоректировкаДолга.ПеренесениеДолга Тогда
		
		Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
		
		Если КорректироватьЧерезСторно Тогда
			Знак = -1;
			Если ВидКорректировки=Перечисления.ВидКорректировки.Начисления Тогда
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Иначе
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			КонецЕсли;
		Иначе
			Знак = 1;
			Если ВидКорректировки=Перечисления.ВидКорректировки.Начисления Тогда
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Иначе
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			КонецЕсли;
		КонецЕсли;
		
		Движение.Период = Дата;
		Движение.ДоговорКонтрагента = ДоговорИсточник;
		Движение.ТипВзаиморасчета = ТипВзаиморасчетов;
		Движение.КБК = КБКИсточник;
		Движение.Контрагент = КонтрагентИсточник;
		Движение.Сумма = Знак*Сумма;
	КонецЕсли;
	
	Движение = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
	Если ВидКорректировки=Перечисления.ВидКорректировки.Начисления Тогда
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Иначе
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЕсли;
	Движение.Период = Дата;
	Движение.ДоговорКонтрагента = Договор;
	Движение.ТипВзаиморасчета = ТипВзаиморасчетов;
	Движение.КБК = КБК;
	Движение.Контрагент = Контрагент;
	Движение.Сумма = Сумма;

КонецПроцедуры                                                   

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ДанныеЗаполнения=Неопределено Тогда
		ВидОперации=Перечисления.ВидыОперацийКоректировкаДолга.КорректировкаДолга;
		ВидКорректировки=Перечисления.ВидКорректировки.Начисления;
		КорректируемыйМесяц=НачалоМесяца(ТекущаяДата());
		Биллинг.ПриВводеНовогоЗаполнитьОрганизацию(ЭтотОбъект);
	КонецЕсли;	
КонецПроцедуры


