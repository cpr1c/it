

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
    
    ПрочитатьДополнительныхИсполнителейВСтрокиЗадач(ТекущийОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры


&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Для Каждого Стр Из Объект.Задачи Цикл
		Стр.КлючСтроки = Строка(Новый УникальныйИдентификатор);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ДополнительныеИсполнители.Очистить();
	
	Для Каждого Стр Из Объект.Задачи Цикл
		Для Каждого СтрДоп Из Стр.ДополнительныеИсполнители Цикл
			НоваяСтрока = ТекущийОбъект.ДополнительныеИсполнители.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрДоп);
			НоваяСтрока.КлючСтроки = Стр.КлючСтроки;
		КонецЦикла;			
	
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура ПериодРегистрацииПриИзменении(Элемент)
	Объект.ПериодРегистрации = НачалоМесяца(Объект.ПериодРегистрации);
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьЗадачи(Команда)
	ЗаполнитьЗадачиНаСервере();
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ЗаполнитьЗадачиНаСервере()
	Объект.Задачи.Очистить();
	
	ЗАпрос=Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	RA_RedmineЗадачи.Ссылка КАК Задача,
	|	RA_RedmineЗадачи.Исполнитель.Пользователь КАК Исполнитель,
	|	RA_RedmineЗадачи.ОценкаТрудозатрат КАК КоличествоЧасов,
	|	ВЫБОР
	|		КОГДА RA_RedmineЗадачи.Статус.Платный
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыОплатыЗадач.Платная)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыОплатыЗадач.Бесплатная)
	|	КОНЕЦ КАК ВидОплаты,
	|	RA_RedmineЗадачи.КонтактОбращения КАК КонтактОбращения
	|ПОМЕСТИТЬ ВыборкаЗадачОсновная
	|ИЗ
	|	Справочник.RA_RedmineЗадачи КАК RA_RedmineЗадачи
	|ГДЕ
	|	RA_RedmineЗадачи.Проект.Партнер = &Партнер
	|	И RA_RedmineЗадачи.Статус.Закрыто
	|	И RA_RedmineЗадачи.Статус.ВидСтатуса = &ВидСтатусаРедмайнаРешено
	|	И RA_RedmineЗадачи.ДатаЗакрытия >= &ДатаНачалаУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Задача.Ссылка,
	|	Задача.Исполнитель,
	|	Задача.ОценкаТрудозатрат,
	|	Задача.ВидОплаты,
	|	Задача.КонтактОбращения
	|ИЗ
	|	Документ.Задача КАК Задача
	|ГДЕ
	|	Задача.Проект.Партнер = &Партнер
	|	И Задача.Статус.Вид В (&ВидыСтатусов)
	|	И (Задача.ДатаЗакрытия >= &ДатаНачалаУчета
	|	ИЛИ Задача.ДатаЗакрытия = ДАТАВРЕМЯ(1, 1, 1))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыборкаЗадачОсновная.Задача КАК Задача,
	|	ВыборкаЗадачОсновная.Исполнитель КАК Исполнитель,
	|	ВыборкаЗадачОсновная.КоличествоЧасов КАК КоличествоЧасов,
	|	ВыборкаЗадачОсновная.ВидОплаты КАК ВидОплаты,
	|	ВыборкаЗадачОсновная.КонтактОбращения КАК КонтактОбращения
	|ИЗ
	|	ВыборкаЗадачОсновная КАК ВыборкаЗадачОсновная
	|ГДЕ
	|	НЕ ВыборкаЗадачОсновная.Задача В
	|				(ВЫБРАТЬ
	|					ФормированиеЗаказаЗадачи.Задача КАК Задача
	|				ИЗ
	|					Документ.ОтчетПоРаботамПартнеру.Задачи КАК ФормированиеЗаказаЗадачи
	|				ГДЕ
	|					ФормированиеЗаказаЗадачи.Ссылка.Проведен
	|					И ФормированиеЗаказаЗадачи.Ссылка <> &ТекущийДокумент)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВыборкаЗадачОсновная";
	Запрос.УстановитьПараметр("Партнер", Объект.Партнер);
	Запрос.УстановитьПараметр("ДатаНачалаУчета", Константы.ДатаНачалаФормированияОтчетовПоРаботамПартнерам.Получить());
	Запрос.УстановитьПараметр("ВидСтатусаРедмайнаРешено", Перечисления.RA_RedmineВидыСтатусовЗадач.Решена);
	Запрос.УстановитьПараметр("ТекущийДокумент",Объект.Ссылка);
	
	ВидыСтатусов = Новый Массив;
	ВидыСтатусов.Добавить(Перечисления.ВидыСтатусовЗадач.Закрыта);
	
	Запрос.УстановитьПараметр("ВидыСтатусов",ВидыСтатусов);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() ЦИкл
		НС=Объект.Задачи.Добавить();
		ЗаполнитьЗначенияСвойств(НС,Выборка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДополнительныхИсполнителейВСтрокиЗадач(ТекущийОбъект)
	Для Каждого Стр Из Объект.Задачи Цикл
		Стр.ДополнительныеИсполнители.Очистить();
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("КлючСтроки", Стр.КлючСтроки);
		
		СтрокиТЧ = ТекущийОбъект.ДополнительныеИсполнители.НайтиСтроки(СтруктураПоиска);
		
		Для Каждого СтрДоп ИЗ СтрокиТЧ Цикл
			НоваяСтрока = Стр.ДополнительныеИсполнители.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрДоп);
		КонецЦикла;		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти