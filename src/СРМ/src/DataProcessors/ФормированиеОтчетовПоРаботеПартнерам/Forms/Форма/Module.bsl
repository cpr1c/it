
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИнициализироватьСхемуКомпоновки();
	УстановитьУсловноеОформлениеФормы();
	
	СтатусЗадачЗакрыт = УправлениеЗадачамиПовтИсп.СтатусЗадачиЗакрыт();
	
	ШкалаТрудозатрат = УчетВремениКлиентСервер.ШкалаЗначенийТрудозатрат();
	Элементы.ЗадачиОценкаТрудозатрат.СписокВыбора.ЗагрузитьЗначения(ШкалаТрудозатрат);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	//TODO: Вставить содержимое обработчика
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверитьЗаполнениеДереваЗадачПередПрименениемИзменений(Задачи, Отказ);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РасчетныйПериодПриИзменении(Элемент)
	РасчетныйПериод = НачалоМесяца(РасчетныйПериод);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗадачи

&НаКлиенте
Процедура ЗадачиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ТекДанные = Элементы.Задачи.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Задачи.ТекущийЭлемент = Элементы.ЗадачиПометка Тогда
		Возврат;
	КонецЕсли;
	
	ТекДанные.Изменено = Истина;
	ТекДанные.Пометка = Истина;
	ЗадачиПометкаПриИзмененииНаСервере(ТекДанные.ПолучитьИдентификатор());
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиПометкаПриИзменении(Элемент)
	ЗадачиПометкаПриИзмененииНаСервере(Элементы.Задачи.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтрокаТЧ = Задачи.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если СтрокаТЧ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "ЗадачиНомер" Тогда
		ПоказатьЗначение(Новый ОписаниеОповещения, СтрокаТЧ.Задача);
	ИначеЕсли Поле.Имя = "ЗадачиФактическиеТрудозатраты" Тогда 
		УправлениеЗадачамиКлиент.ОткрытьТрудозатратыПоЗадаче(СтрокаТЧ.Задача);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиОценкаТрудозатратРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ТекДанные = Элементы.Задачи.ТекущиеДанные;
	Если ТекДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;
	УправлениеЗадачамиКлиент.ИзменитьЗначениеПоляОценкаТрудозатратПоНаправлениюРегулирования(ТекДанные.ОценкаТрудозатрат, Направление);
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьСписокЗадач(Команда)
	ОчиститьСообщения();
	ЗаполнитьДеревоЗадач();
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИзмененияЗадач(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	ПрименитьИзмененияЗадачНаСервере();
	
	Оповестить("ИзмененОтчетПоРаботамСПартнерами");
КонецПроцедуры

&НаКлиенте
Процедура СвернутьДеревоЗадач(Команда)
	Для Каждого СтрокаТЧ Из Задачи.ПолучитьЭлементы() Цикл
		Элементы.Задачи.Свернуть(СтрокаТЧ.ПолучитьИдентификатор());
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьДеревоЗадач(Команда)
	Для Каждого СтрокаТЧ Из Задачи.ПолучитьЭлементы() Цикл
		Элементы.Задачи.Развернуть(СтрокаТЧ.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьЗаполнениеДереваЗадачПередПрименениемИзменений(СтрокаДерева, Отказ) 
	Для Каждого ТекСтрока Из СтрокаДерева.ПолучитьЭлементы() Цикл
		Если Не ТекСтрока.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекСтрока.Уровень = 2 Тогда
			Если Не ЗначениеЗаполнено(ТекСтрока.Партнер) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не заполнен партнер по задаче " + ТекСтрока.Номер,
					, "ЗадачиПартнер", , Отказ);
			КонецЕсли;
		КонецЕсли;
	
		ПроверитьЗаполнениеДереваЗадачПередПрименениемИзменений(ТекСтрока, Отказ);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСхемуКомпоновки()
	СКД = Обработки.ФормированиеОтчетовПоРаботеПартнерам.ПолучитьМакет("СхемаДляОтбораЗадач");
	АдресСхемы = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных();
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы));
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
	ЗаполнитьНастройкиПоУмолчанию();
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуПодчиненныхСтрок(ТекущаяСтрока, Пометка)
	Для Каждого СтрокаТЧ ИЗ ТекущаяСтрока.ПолучитьЭлементы() Цикл
		УстановитьПометкуПодчиненныхСтрок(СтрокаТЧ, Пометка);
		
		СтрокаТЧ.Пометка = Пометка;

		ПриУстановкеПометкиСтроки(СтрокаТЧ);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьПометкуРодительскихЭлементов(ТекущаяСтрока)
	Родитель = ТекущаяСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	ЕстьПометка = Ложь;
	Для Каждого Стр ИЗ Родитель.ПолучитьЭлементы() Цикл
		Если Стр.Пометка Тогда
			ЕстьПометка = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Родитель.Пометка = ЕстьПометка;
	
	ПриУстановкеПометкиСтроки(Родитель);
	
	УстановитьПометкуРодительскихЭлементов(Родитель);
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиСтрокиДерева(СтрокаДерева)
	Для Каждого Стр ИЗ СтрокаДерева.ПолучитьЭлементы() Цикл
		Если Не Стр.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Если Стр.Уровень = 2 Тогда
			ИтогоОценкаПоВыбраннымЗадачам = ИтогоОценкаПоВыбраннымЗадачам + Стр.ОценкаТрудозатрат;
			ИтогоТрудозатратыПоВыбраннымЗадачам = ИтогоТрудозатратыПоВыбраннымЗадачам + Стр.ФактическиеТрудозатраты;
		Иначе
			ПересчитатьИтогиСтрокиДерева(Стр);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтоги()
	ИтогоОценкаПоВыбраннымЗадачам = 0;
	ИтогоТрудозатратыПоВыбраннымЗадачам = 0;	
	
	ПересчитатьИтогиСтрокиДерева(Задачи);
КонецПроцедуры

&НаСервере
Процедура ПриУстановкеПометкиСтроки(СтрокаТЧ)
	Если СтрокаТЧ.Уровень<>2 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не СтрокаТЧ.Пометка Тогда
		Возврат;
	КонецЕсли;
	
	ВидыФинальныхСтатусов = ВидыФинальныхСтатусов();
	
	Если ВидыФинальныхСтатусов.Найти(СтрокаТЧ.Статус.Вид) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТЧ.Статус = СтатусЗадачЗакрыт;
	СтрокаТЧ.Изменено = Истина;
	
КонецПроцедуры


&НаСервере
Процедура ЗадачиПометкаПриИзмененииНаСервере(ТекущаяСтрока)
	СтрокаТЧ = Задачи.НайтиПоИдентификатору(ТекущаяСтрока);
	Если СтрокаТЧ = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПриУстановкеПометкиСтроки(СтрокаТЧ);

	Если ВидыФинальныхСтатусов().Найти(СтрокаТЧ.Статус.Вид) = Неопределено 
		И СтрокаТЧ.Уровень=2 Тогда
		СтрокаТЧ.Пометка = Ложь;
	КонецЕсли;

	УстановитьПометкуПодчиненныхСтрок(СтрокаТЧ, СтрокаТЧ.Пометка);
	УстановитьПометкуРодительскихЭлементов(СтрокаТЧ);
	ПересчитатьИтоги();
КонецПроцедуры




&НаСервере
Процедура ДобавитьЗадачуВДанныеДляОтчетовПоРаботам(СтрокаДерева, СоответствиеДанныхОтчетовПоРаботам)
	СтрокиПоПартнеру = СоответствиеДанныхОтчетовПоРаботам[СтрокаДерева.Партнер];
	Если СтрокиПоПартнеру = Неопределено Тогда
		СоответствиеДанныхОтчетовПоРаботам.Вставить(СтрокаДерева.Партнер, Новый Массив);
		СтрокиПоПартнеру = СоответствиеДанныхОтчетовПоРаботам[СтрокаДерева.Партнер];
	КонецЕсли;	
	
	СтрокиПоПартнеру.Добавить(СтрокаДерева.ПолучитьИдентификатор());
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияЗадачи(СтрокаДерева, СоответствиеДанныхОтчетовПоРаботам)
	Если СтрокаДерева.Изменено Тогда
		ЗадачаОбъект = СтрокаДерева.Задача.ПолучитьОбъект();

		ПоляИзменяемые = Новый Массив;
		ПоляИзменяемые.Добавить("Тема");
		ПоляИзменяемые.Добавить("КонтактОбращения");
		ПоляИзменяемые.Добавить("ВидОплаты");
		ПоляИзменяемые.Добавить("Статус");
		ПоляИзменяемые.Добавить("ОценкаТрудозатрат");
		ПоляИзменяемые.Добавить("Исполнитель");
		ПоляИзменяемые.Добавить("Спринт");
		ПоляИзменяемые.Добавить("КатегорияЗакрытия");

		ЗаполнитьЗначенияСвойств(ЗадачаОбъект, СтрокаДерева, СтрСоединить(ПоляИзменяемые, ","));

		Если Не ЗадачаОбъект.ПроверитьЗаполнение() Тогда
			Сообщить("Не все поля заполнены правильно для задачи "+ЗадачаОбъект);
			Возврат;
		КонецЕсли;

		Попытка
			ЗадачаОбъект.Записать();
			СтрокаДерева.Изменено = Ложь;

			ЗаполнитьЗначенияСвойств(СтрокаДерева, ЗадачаОбъект, "ДатаВыполнения,ДатаЗакрытия");

		Исключение
			Сообщить("Не удалось обновить данные по задаче " + ЗадачаОбъект + ": " + ОписаниеОшибки());
			Возврат;
		КонецПопытки;
	КонецЕсли;

	ДобавитьЗадачуВДанныеДляОтчетовПоРаботам(СтрокаДерева, СоответствиеДанныхОтчетовПоРаботам);

КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияПоДеревуЗадач(Дерево, СоответствиеДанныхОтчетовПоРаботам)
	Для Каждого СтрокаТЧ Из Дерево.ПолучитьЭлементы() Цикл
		Если Не СтрокаТЧ.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТЧ.Уровень<>2 Тогда
			ПрименитьИзмененияПоДеревуЗадач(СтрокаТЧ, СоответствиеДанныхОтчетовПоРаботам);
		Иначе
			ПрименитьИзмененияЗадачи(СтрокаТЧ, СоответствиеДанныхОтчетовПоРаботам);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетыПоРаботе(СоответствиеДанныхОтчетовПоРаботам)
	Для Каждого КлючЗначение Из СоответствиеДанныхОтчетовПоРаботам Цикл
		ОтчетПоРаботам = Документы.ОтчетПоРаботамПартнеру.ТекущийОтчетПоРаботамПартнера(КлючЗначение.Ключ, РасчетныйПериод);
		Если ЗначениеЗаполнено(ОтчетПоРаботам) Тогда
			ОтчетПоРаботамОбъект = ОтчетПоРаботам.ПолучитьОбъект();
		Иначе
			ОтчетПоРаботамОбъект = Документы.ОтчетПоРаботамПартнеру.СоздатьДокумент();
			ОтчетПоРаботамОбъект.Партнер = КлючЗначение.Ключ;
			ОтчетПоРаботамОбъект.Дата = ТекущаяДатаСеанса();
			ОтчетПоРаботамОбъект.РасчетныйПериод = РасчетныйПериод;
			ОтчетПоРаботамОбъект.Заполнить(Неопределено);
			ОтчетПоРаботамОбъект.УстановитьНовыйНомер();
			ОтчетПоРаботамОбъект.Комментарий = "Сформирован автоматически обработкой формирования отчетов по партнерам "+ТекущаяДатаСеанса();
		КонецЕсли;
		
		Для Каждого ИдентификаторСтрокиДерева Из КлючЗначение.Значение Цикл
			СтрокаДерева = Задачи.НайтиПоИдентификатору(ИдентификаторСтрокиДерева);
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Задача", СтрокаДерева.Задача);
			НайденныеСтроки = ОтчетПоРаботамОбъект.Задачи.НайтиСтроки(СтруктураПоиска);
			Если НайденныеСтроки.Количество()>0 Тогда
				Сообщить("Задача "+СтрокаДерева.Задача+" была добавлена ранее в отчет");
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ОтчетПоРаботамОбъект.Задачи.Добавить();
			НоваяСтрока.Задача = СтрокаДерева.Задача;
			НоваяСтрока.Исполнитель = СтрокаДерева.Исполнитель;
			НоваяСтрока.ВидОплаты = СтрокаДерева.ВидОплаты;
			НоваяСтрока.КоличествоЧасов = СтрокаДерева.ОценкаТрудозатрат;
			НоваяСтрока.КонтактОбращения = СтрокаДерева.КонтактОбращения;
		КонецЦикла;
		
		Попытка
			ОтчетПоРаботамОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			Сообщить("Не удалось добавить задачи по партнеру "+КлючЗначение.Ключ+" в отчет по нему");
			Продолжить;
		КонецПопытки;
		
		Для Каждого ИдентификаторСтрокиДерева Из КлючЗначение.Значение Цикл
			СтрокаДерева = Задачи.НайтиПоИдентификатору(ИдентификаторСтрокиДерева);
			СтрокаДерева.ДобавленаВОтчет = Истина;
			СтрокаДерева.Пометка = Ложь;
			УстановитьПометкуРодительскихЭлементов(СтрокаДерева);
		КонецЦикла;
		
	КонецЦикла;	
	
	ПересчитатьИтоги();
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияЗадачНаСервере()
	СоответствиеДанныхОтчетовПоРаботам = Новый Соответствие;
	
	ПрименитьИзмененияПоДеревуЗадач(Задачи, СоответствиеДанныхОтчетовПоРаботам);
	СформироватьОтчетыПоРаботе(СоответствиеДанныхОтчетовПоРаботам);
	
КонецПроцедуры


&НаСервере
Процедура УстановитьУсловноеОформлениеФормы()
	УсловноеОформление.Элементы.Очистить();
	
	// Уровень исполнителя
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 0);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);	

	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиНомер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиТема");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПартнер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПроект");
	
	// Уровень проект
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 1);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);	

	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиНомер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиТема");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПартнер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиИсполнитель");
	
	// Измененные строки
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Изменено", Истина);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.СветлоЗолотистый);	

	ДобавитьПолеОформления(НовыйЭлементОформления, "Задачи");
	
	//Разрешаем редактировать только уровень задач
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 2,
		ВидСравненияКомпоновкиДанных.НеРавно);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиНомер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиТема");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПартнер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиИсполнитель");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиКонтактОбращения");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиВидОплаты");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиКатегорияЗакрытия");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиСтатус");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиСпринт");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиОценкаТрудозатрат");

	
	//Разрешаем редактировать только уровень задач
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.ДобавленаВОтчет", Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 2);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	ДобавитьПолеОформления(НовыйЭлементОформления, "Задачи");
	
	// Оформление второй строки для задачи
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 2);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(, , , Истина, , , ));

	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПроект");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПартнер");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиИсполнитель");
	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиДатаЗакрытия");
	
	// Оформление второй строки для задачи
	НовыйЭлементОформления = УсловноеОформление.Элементы.Добавить();
	НовыйЭлементОформления.Использование = Истина;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлементОформления.Отбор, "Задачи.Уровень", 2);
	НовыйЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	ДобавитьПолеОформления(НовыйЭлементОформления, "ЗадачиПроект");
	
КонецПроцедуры

&НаСервере
Функция ВидыФинальныхСтатусов()
	Виды = Новый Массив;
	Виды.Добавить(Перечисления.ВидыСтатусовЗадач.Закрыта);
	Виды.Добавить(Перечисления.ВидыСтатусовЗадач.Отклонена);
	
	Возврат Виды;
КонецФункции

&НаСервере
Процедура ДобавитьПолеОформления(ЭлементОформления, ИмяПоля)
	НовоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	НовоеПоле.Использование = Истина;
	НовоеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
КонецПроцедуры

&НаСервере
Процедура ВывестиДеревоЗадачПоДеревуЗапроса(ДеревоЗапроса, ДеревоЗадач, Уровень = 0)
	Для Каждого СтрокаДерева Из ДеревоЗапроса.Строки  Цикл
		СтрокаДереваЗадач = ДеревоЗадач.ПолучитьЭлементы().Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДереваЗадач, СтрокаДерева);
		СтрокаДереваЗадач.Уровень = Уровень;
		
		ВывестиДеревоЗадачПоДеревуЗапроса(СтрокаДерева, СтрокаДереваЗадач, Уровень+1);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоЗадач()
	Задачи.ПолучитьЭлементы().Очистить();

	СКД = ПолучитьИзВременногоХранилища(АдресСхемы);
	НастройкиСКД = КомпоновщикНастроек.ПолучитьНастройки();
	
	НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра("ДатаНачалаУчета", Константы.ДатаНачалаФормированияОтчетовПоРаботамПартнерам.Получить());
	
	КомпоновщикМакетаКомпоновкиДанных = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакетаКомпоновкиДанных.Выполнить(СКД, НастройкиСКД, ,
		, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	Дерево = Новый ДеревоЗначений();
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
	ПроцессорВывода.УстановитьОбъект(Дерево);
	
	Дерево = ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ВывестиДеревоЗадачПоДеревуЗапроса(Дерево, Задачи);
	
	ПересчитатьИтоги();

КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНастройкиПоУмолчанию()
	
	Запрос = Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ
	|	СтатусыЗадач.Ссылка,
	|	СтатусыЗадач.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	СтатусыЗадач.Наименование КАК Наименование
	|ИЗ
	|	Справочник.СтатусыЗадач КАК СтатусыЗадач
	|ГДЕ
	|	СтатусыЗадач.Вид В (&Вид)
	|	И НЕ СтатусыЗадач.ПометкаУдаления
	|УПОРЯДОЧИТЬ ПО
	|	РеквизитДопУпорядочивания,
	|	Наименование";
	ВидыСтатусов = Новый Массив;
	ВидыСтатусов.Добавить(Перечисления.ВидыСтатусовЗадач.Выполнена);
	ВидыСтатусов.Добавить(Перечисления.ВидыСтатусовЗадач.Закрыта);
	ВидыСтатусов.Добавить(Перечисления.ВидыСтатусовЗадач.Отклонена);
	Запрос.УстановитьПараметр("Вид", ВидыСтатусов);
	
	Статусы = Новый СписокЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Статусы.Добавить(Выборка.Ссылка);
	КонецЦикла;

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(КомпоновщикНастроек.Настройки.Отбор, "Статус", Статусы,
		ВидСравненияКомпоновкиДанных.ВСписке);
		
КонецПроцедуры

#КонецОбласти