
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("ЗагрузкаИзФайла");
	ТабДокумент.Вывести(Макет);
	
	Объект.Исполнитель = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура Прочитать(Команда)
	
	ПрочитатьНаСервере();
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаПредварительныйПросмотр;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	
	Объект.ЗагружаемыеДанные.Очистить();
	
	Для НомерСтроки = 2 По ТабДокумент.ВысотаТаблицы Цикл
		
		НоваяСтрока = Объект.ЗагружаемыеДанные.Добавить();
		
		НоваяСтрока.Тема = СокрЛП(ТабДокумент.Область(НомерСтроки, 1).Текст);
		НоваяСтрока.Проект = Справочники.Проекты.НайтиПоНаименованию(СокрЛП(ТабДокумент.Область(НомерСтроки, 2).Текст), Истина);
		НоваяСтрока.Исполнитель = Справочники.Пользователи.НайтиПоНаименованию(СокрЛП(ТабДокумент.Область(НомерСтроки, 3).Текст), Истина);
		НоваяСтрока.КонтактОбращения = Справочники.ФизическиеЛица.НайтиПоНаименованию(СокрЛП(ТабДокумент.Область(НомерСтроки, 4).Текст), Истина);
		НоваяСтрока.ОценкаТрудозатрат = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(ТабДокумент.Область(НомерСтроки, 5).Текст));
		НоваяСтрока.ОценкаТрудозатратИсполнителя = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(ТабДокумент.Область(НомерСтроки, 6).Текст));
		НоваяСтрока.РодительскаяЗадача = Документы.Задача.НайтиПоНомеру(СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СокрЛП(ТабДокумент.Область(НомерСтроки, 7).Текст)));
		НоваяСтрока.Спринт = СпринтПоНаименованию(СокрЛП(ТабДокумент.Область(НомерСтроки, 8).Текст));
		НоваяСтрока.СрокИсполнения = СтроковыеФункцииКлиентСервер.СтрокаВДату(СокрЛП(ТабДокумент.Область(НомерСтроки, 9).Текст));
		НоваяСтрока.Срочность = Справочники.СрочностиЗадач.НайтиПоНаименованию(СокрЛП(ТабДокумент.Область(НомерСтроки, 10).Текст), Истина);
		НоваяСтрока.Содержание = СокрЛП(ТабДокумент.Область(НомерСтроки, 11).Текст);
		Примечание = СокрЛП(ТабДокумент.Область(НомерСтроки, 12).Текст);
		Если ЗначениеЗаполнено(Примечание) Тогда
			НоваяСтрока.Содержание = НоваяСтрока.Содержание + " (" + Примечание + ")";
		КонецЕсли;
		
		// значения по умолчанию
		Если Не ЗначениеЗаполнено(НоваяСтрока.Проект) Тогда
			НоваяСтрока.Проект = Объект.Проект;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.Исполнитель) Тогда
			НоваяСтрока.Исполнитель = Объект.Исполнитель;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.КонтактОбращения) Тогда
			НоваяСтрока.КонтактОбращения = Объект.КонтактОбращения;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.РодительскаяЗадача) Тогда
			НоваяСтрока.РодительскаяЗадача = Объект.РодительскаяЗадача;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.Спринт) Тогда
			НоваяСтрока.Спринт = Объект.Спринт;	
		КонецЕсли;
		Если Не ЗначениеЗаполнено(НоваяСтрока.Срочность) Тогда
			НоваяСтрока.Срочность = Объект.Срочность;	
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	СоздатьДокументыНаСервере();
	ПоказатьПредупреждение(,"Обработка завершена");
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		НачатьТранзакцию();
		
		МассивДанныхЗаполнения = ОбщегоНазначения.ТаблицаЗначенийВМассив(Объект.ЗагружаемыеДанные.Выгрузить());
		
		Для Каждого ДанныеЗаполнения Из МассивДанныхЗаполнения Цикл
			
			ЗадачаОбъект = Документы.Задача.СоздатьДокумент();
			ЗадачаОбъект.Заполнить(ДанныеЗаполнения);

			ЗадачаОбъект.Записать(РежимЗаписиДокумента.Проведение);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Причина = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(Причина);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция СпринтПоНаименованию(Наименование)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 2
	|	Спринт.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.Спринт КАК Спринт
	|ГДЕ
	|	Спринт.Наименование = &Наименование
	|	И НЕ Спринт.Завершен
	|	И НЕ Спринт.ПометкаУдаления";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Рез = Запрос.Выполнить().Выгрузить();
	Если Рез.Количество() = 1 Тогда
		Возврат Рез[0].Ссылка;
	Иначе
		Возврат Документы.Спринт.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции