Процедура ПриНачалеРаботыСистемы() Экспорт
	
	ГлобальныйПоискКлиент.ПриНачалеРаботыСистемы();
	
	ОткрыватьФормуРаботыСЗадачамиПриЗапуске = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить(
		"ОбщиеНастройкиПользователя", 
		"ОткрыватьФормуРаботыСЗадачамиПриЗапуске");

	Если ОткрыватьФормуРаботыСЗадачамиПриЗапуске = Истина Тогда
		ОткрытьФорму("Обработка.АРМСпециалистаПоддержки.Форма.Форма");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьТрудозатратыПоЗадаче(Задача) Экспорт
	
	Отбор=Новый Структура;
	Отбор.Вставить("Предмет",Задача);
	
	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("Отбор",Отбор);
	
	ОткрытьФорму("Документ.Трудозатраты.ФормаСписка",ПараметрыФормы,,Задача);
	
КонецПроцедуры

Процедура РедактироватьКопииПолучателейСобытияЗадачи(ТаблицаПолучателей,ОписаниеОповещения=Неопределено) Экспорт
	Если ОписаниеОповещения=Неопределено Тогда
		ДополнительныеПараметры=Новый Структура;
		ДополнительныеПараметры.Вставить("ТаблицаПолучателей",ТаблицаПолучателей);
		
		ОписаниеОповещения=Новый ОписаниеОповещения("РедактироватьКопииПолучателейСобытияЗадачиОкончание",ЭтотОбъект,ДополнительныеПараметры);
	КонецЕсли;
	
	МассивПолучателей=Новый Массив;
	
	Для каждого Стр Из ТаблицаПолучателей Цикл
		ПолучательКопии=Новый Структура("Адрес,Представление,Контакт,ВариантОтправки");
		ЗаполнитьЗначенияСвойств(ПолучательКопии,Стр);
		
		МассивПолучателей.Добавить(ПолучательКопии);
	КонецЦикла;
	
	ПараметрыФормы=Новый Структура;
	ПараметрыФормы.Вставить("Получатели",МассивПолучателей);
	
	ОткрытьФорму("ОбщаяФорма.РедакторПолучателейПочты",ПараметрыФормы,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
КонецПроцедуры

Процедура РедактироватьКопииПолучателейСобытияЗадачиОкончание(Результат,ДополнительныеПараметры) Экспорт
	Если Результат=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	
	ТаблицаПолучателей=ДополнительныеПараметры.ТаблицаПолучателей;
	
	ТаблицаПолучателей.Очистить();
	
	Для Каждого Стр Из Результат ЦИкл
		НС=ТаблицаПолучателей.Добавить();
		ЗаполнитьЗначенияСвойств(НС,Стр);
	КонецЦикла;
	
//	Оповестить("ИзменилисьДопАдресаДляЭлектроннойПочты",ДополнительныеПараметры.ИдентификаторФормыИсточника);
	
КонецПроцедуры

Процедура ПолеЧасовРегулирование(Форма, ПутьКДанным, Направление, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ТекущееЗначение = Вычислить("Форма."+ПутьКДанным);
	Квант = 0.25;
	
	// приводим текущее значение к точности 0.25
	// добавляем (отнимаем) 0.25
	// должно получиться не меньше 0.25
	НовоеЗначение = Макс(Окр(ТекущееЗначение / Квант, 0) * Квант + Квант * Направление, Квант);
	
	Выполнить("Форма."+ПутьКДанным+ " = " + Формат(НовоеЗначение, "ЧРД=.; ЧН=0; ЧГ=0"));
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура ПолеОценкаИсполнителяРегулирование(Форма, ПутьКДанным, Направление, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Ряд = УправлениеЗадачамиКлиентСервер.РядФибоначчи();
	
	ТекущееЗначение = Вычислить("Форма."+ПутьКДанным);
	
	ТекущееЗначение = УправлениеЗадачамиКлиентСервер.ОценкаПоРядуФибоначчи(ТекущееЗначение);
	
	ИндексТекущегоЗначения = Ряд.Найти(ТекущееЗначение);
	
	Если ИндексТекущегоЗначения = Неопределено Тогда
		ИндексТекущегоЗначения = 0;
	КонецЕсли;
	
	ИндексНовогоЗначения = Макс(0, ИндексТекущегоЗначения + Направление);
	ИндексНовогоЗначения = Мин(ИндексНовогоЗначения, 99);
	
	НовоеЗначение = Ряд[ИндексНовогоЗначения];
	
	Выполнить("Форма."+ПутьКДанным+ " = " + Формат(НовоеЗначение, "ЧРД=.; ЧН=0; ЧГ=0"));
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

Процедура ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаПрисоединенныйФайл,УникальныйИдентификатор) Экспорт
	ДанныеФайла = РаботаСФайламиСлужебныйВызовСервера.ДанныеФайлаДляОткрытия(СсылкаНаПрисоединенныйФайл, Неопределено, УникальныйИдентификатор);
	Если ДанныеФайла.Зашифрован Тогда
		// Файл может быть изменен в другом сеансе.
		ОповеститьОбИзменении(СсылкаНаПрисоединенныйФайл);
		Возврат;
	КонецЕсли;
	
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла,Ложь);

КонецПроцедуры

#Область ИсторияЗадачи
Процедура ОбновитьИсториюЗадачи(Задача,ЭлементФормыИстории,Форма,ДанныеПрисоединенныхФайлов) Экспорт 
	МассивИстории=УправлениеЗадачами.ПолучитьМассивИсторииЗадачи(Задача,Форма.УникальныйИдентификатор,ДанныеПрисоединенныхФайлов);
	
	ДокументИстории=ЭлементФормыИстории.Документ.defaultView;
	
	МассивПередачи=Новый Массив;
	ДокументИстории.appTo1C.setBaseURL(ПолучитьНавигационнуюСсылкуИнформационнойБазы()+"/ru");
	
	Для Каждого ТекЗаписьИстории ИЗ МассивИстории Цикл
		СтруктураЗаписи=Новый Структура;
		СтруктураЗаписи.Вставить("id", Строка(ТекЗаписьИстории.Ссылка.УникальныйИдентификатор()));
		СтруктураЗаписи.Вставить("date", Формат(ТекЗаписьИстории.ДатаСоздания,"ДФ='dd.MM.yyyy ЧЧ:мм'"));
		СтруктураЗаписи.Вставить("title", ТекЗаписьИстории.Заголовок);
		СтруктураЗаписи.Вставить("text", ТекЗаписьИстории.ТекстСообщения);
		СтруктураЗаписи.Вставить("type", ТекЗаписьИстории.Формат);
		
		Автор=Новый Структура;
		Автор.Вставить("name", Строка(ТекЗаписьИстории.Автор));
//		Автор.Вставить("avatar", АдресКартинки);
		Автор.Вставить("email", ТекЗаписьИстории.АвторАдресЭлектроннойПочты);
		СтруктураЗаписи.Вставить("author", Автор);
		
		СтруктураЗаписи.Вставить("attachments", Новый Массив);
		
		Для Каждого ТекВложение Из ТекЗаписьИстории.МассивВложений Цикл
			СтруктураВложения=Новый Структура;
			СтруктураВложения.Вставить("id", Строка(ТекВложение.Ссылка.УникальныйИдентификатор()));
			СтруктураВложения.Вставить("filename",ТекВложение.ИмяФайла);
			СтруктураВложения.Вставить("href",ПолучитьНавигационнуюСсылку(ТекВложение.Ссылка));
			СтруктураЗаписи.attachments.Добавить(СтруктураВложения);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(ТекЗаписьИстории.Основание) Тогда
			Основание=Новый Структура;
			Основание.Вставить("title",Строка(ТекЗаписьИстории.Основание));
			Основание.Вставить("url",ПолучитьНавигационнуюСсылку(ТекЗаписьИстории.Основание));
			
			СтруктураЗаписи.Вставить("base",Основание);
		КонецЕсли;
		
		МассивПередачи.Добавить(СтруктураЗаписи);
	КонецЦикла;
	
	ДокументИстории.appTo1C.history.update(ОбщегоНазначенияКлиентСервер.ЗаписатьДанныеJSON(МассивПередачи));
КонецПроцедуры

Процедура ПриНажатииПоляИсторииЗадачи(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка=Ложь;

	Если СтрНачинаетсяС(ДанныеСобытия.Element.id, "Файл_") Тогда
		ИдентификаторФайла=СтрЗаменить(ДанныеСобытия.Element.id, "Файл_", "");
		СсылкаНаФайл=УправлениеЗадачами.ПолучитьСсылкуНаПрисоединенныйФайлПоИдентификатору(
			Новый УникальныйИдентификатор(ИдентификаторФайла));
		ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаФайл, Форма.УникальныйИдентификатор);
	ИначеЕсли ДанныеСобытия.Element.name="BASE_SELECTION" Тогда 
		ОбщегоНазначенияКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.href);
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		
		ФайловаяСистемаКлиент.ЗапуститьПрограмму(ДанныеСобытия.href);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

