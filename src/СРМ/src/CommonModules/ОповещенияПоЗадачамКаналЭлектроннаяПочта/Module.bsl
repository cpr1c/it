Процедура ВыполнитьОтправкуСообщения(Сообщение, УчетнаяЗаписьОповещений, ОписаниеОшибки, НастройкиОтправкиОповещения) Экспорт

	УчетнаяЗаписьЭлектроннойПочты=УчетнаяЗаписьОповещений.Настройка;
	Если Сообщение.Получатель.Количество() = 0 Тогда
		ОписаниеОшибки  = НСтр(
			"ru = 'Сообщение не может быть отправлено сразу, т.к необходимо ввести адрес электронной почты.'");
		Возврат;
	КонецЕсли;

	ПараметрыПисьма = Новый Структура;
	ПараметрыПисьма.Вставить("Тема", Сообщение.Тема);
	ПараметрыПисьма.Вставить("Тело", Сообщение.Текст);
	ПараметрыПисьма.Вставить("Вложения", Новый Соответствие);
	ПараметрыПисьма.Вставить("Кодировка", "utf-8");

	Для Каждого Вложение Из Сообщение.Вложения Цикл
		НовоеВложение = Новый Структура("ДвоичныеДанные, Идентификатор");
		НовоеВложение.ДвоичныеДанные = ПолучитьИзВременногоХранилища(Вложение.АдресВоВременномХранилище);
		НовоеВложение.Идентификатор = Вложение.Идентификатор;
		ПараметрыПисьма.Вложения.Вставить(Вложение.Представление, НовоеВложение);
	КонецЦикла;

	МодульРаботаСПочтовымиСообщениямиСлужебный = ОбщегоНазначения.ОбщийМодуль(
			"РаботаСПочтовымиСообщениямиСлужебный");
	Если Сообщение.ДополнительныеПараметры.ФорматПисьма = Перечисления.СпособыРедактированияЭлектронныхПисем.HTML Тогда
		ТипТекста = МодульРаботаСПочтовымиСообщениямиСлужебный.ТипТекстовЭлектронныхПисем("HTMLСКартинками");
	Иначе
		ТипТекста = МодульРаботаСПочтовымиСообщениямиСлужебный.ТипТекстовЭлектронныхПисем("ПростойТекст");
	КонецЕсли;

	ПараметрыПисьма.Вставить("ТипТекста", ТипТекста);

	ПараметрыПисьма.Вставить("Кому", Новый Массив);
	ПараметрыПисьма.Вставить("Копии", Новый Массив);
	ПараметрыПисьма.Вставить("СкрытыеКопии", Новый Массив);

	Для Каждого Получатель Из Сообщение.Получатель Цикл
		МассивДобавления=ПараметрыПисьма.Кому;
		Если Получатель.ВариантОтправки = "Копия:" Тогда
			МассивДобавления=ПараметрыПисьма.Копии;
		ИначеЕсли Получатель.ВариантОтправки = "Скрытая копия:" Тогда
			МассивДобавления=ПараметрыПисьма.СкрытыеКопии;
		КонецЕсли;

		ПолучательСообщения = Новый Структура;
		ПолучательСообщения.Вставить("Представление", Получатель.Представление);
		ПолучательСообщения.Вставить("Адрес", Получатель.Адрес);
		ПолучательСообщения.Вставить("Контакт", Получатель.ИсточникКонтактнойИнформации);

		МассивДобавления.Добавить(ПолучательСообщения);
	КонецЦикла;
	
	Если ПараметрыПисьма.Кому.Количество() = 0 
		И Сообщение.Получатель.Количество()> 0 Тогда
		Получатель=Сообщение.Получатель[0]; 
		
		ПолучательСообщения = Новый Структура;
		ПолучательСообщения.Вставить("Представление", Получатель.Представление);
		ПолучательСообщения.Вставить("Адрес", Получатель.Адрес);
		ПолучательСообщения.Вставить("Контакт", Получатель.ИсточникКонтактнойИнформации);
		
		ПараметрыПисьма.Кому.Добавить(ПолучательСообщения);
	КонецЕсли;

	Если НастройкиОтправкиОповещения.УстанавливатьОтправителемУведомленияАвтораСобытия Тогда
		ПараметрыПисьма.Вставить("ИмяОтправителя", Строка(НастройкиОтправкиОповещения.Автор));
	КонецЕсли;

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		МодульРаботаСПочтовымиСообщениями = ОбщегоНазначения.ОбщийМодуль("РаботаСПочтовымиСообщениями");
		МодульРаботаСПочтовымиСообщениями.ОтправитьПочтовоеСообщение(УчетнаяЗаписьЭлектроннойПочты, ПараметрыПисьма);
	КонецЕсли;
КонецПроцедуры

Функция АдресПолучателя(Получатель) Экспорт
	Возврат УправлениеЗадачами.АдресЭлектроннойПочтыОбъекта(Получатель);
КонецФункции

Функция ТекстИзмененийДляСообщения(Изменения) Экспорт
	ТекстИзменений="";

	Если Изменения.Количество() > 0 Тогда
		ТекстИзменений="<hr>" + Символы.ПС + "<ul>" + Символы.ПС;
		Для Каждого ТекИзменение Из Изменения Цикл
			ТекстИзменений=ТекстИзменений + "<li>Параметр <strong>" + ТекИзменение.Значение.Синоним + "</strong> изменился с <i>"
				+ СтрокаСЭкранированнымиСимволамиHTML(ТекИзменение.Значение.ПредыдущееЗначение) + "</i> на <i>"
				+ СтрокаСЭкранированнымиСимволамиHTML(ТекИзменение.Значение.Значение) + "</i></li>" + Символы.ПС;
		КонецЦикла;

		ТекстИзменений=ТекстИзменений + "</ul>";

	КонецЕсли;
	Возврат ТекстИзменений;
КонецФункции

Функция СтрокаСЭкранированнымиСимволамиHTML(Строка)
	НовСтр= СтрЗаменить(СокрЛП(Строка), "<", "&lt;");
	НовСтр= СтрЗаменить(НовСтр, ">", "&gt;");
	Возврат НовСтр;
КонецФункции

Функция ТекстКомментарияПреобразованный(Инициатор, КомментарийИзБазы, Сообщение) Экспорт
	Если Сообщение.ДополнительныеПараметры.ФорматКомментария = Перечисления.ФорматыТекстаКомментариев.Markdown Тогда
		Комментарий= Сообщение.ДополнительныеПараметры.КомментарийHTML;
	Иначе
		Комментарий=КомментарийИзБазы;
	КонецЕсли;

	ВсеПрисоединенныеФайлы = Новый Массив;
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Сообщение.ДополнительныеПараметры.Задача,
		ВсеПрисоединенныеФайлы);

	Для Каждого ПрисоединенныйФайл Из ВсеПрисоединенныеФайлы Цикл
		Если ТипЗнч(Инициатор) = Тип("ДокументСсылка.Задача") И ПрисоединенныйФайл.Предмет = Неопределено Тогда

		ИначеЕсли ПрисоединенныйФайл.Предмет <> Инициатор Тогда
			Продолжить;
		КонецЕсли;
		СтрокаПоиска="src=""cid:" + Строка(ПрисоединенныйФайл) + """";
		Если СтрНайти(Комментарий, СтрокаПоиска) > 0 Тогда
			ИдентификаторВложения=Строка(Новый УникальныйИдентификатор);
			Комментарий=СтрЗаменить(Комментарий, СтрокаПоиска, "src=""cid:" + ИдентификаторВложения + """");

			ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);

			Сообщение.Вложения.Вставить(ИдентификаторВложения, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
		Иначе
			ДанныеФайла = РаботаСФайлами.ДанныеФайла(ПрисоединенныйФайл);

			Сообщение.Вложения.Вставить(ДанныеФайла.ИмяФайла, ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);
			
		КонецЕсли;

	КонецЦикла;

	Комментарий= "<!DOCTYPE html PUBLIC ""-//W3C//DTD HTML 4.0 Transitional//EN"">
				 |	<html>
				 |		<head>
				 |			<meta http-equiv=""Content-Type"" content=""text/html; charset=utf-8""></meta>
				 |			<meta http-equiv=""X-UA-Compatible"" content=""IE=Edge""></meta>
				 |			<meta name=""format-detection"" content=""telephone=no""></meta>
				 |			<style type=""text/css"">
				 |				body{margin:0;padding:8px;}
				 |				p{line-height:1.15;margin:0;white-space:pre-wrap;}
				 |				ol,ul{margin-top:0;margin-bottom:0;}
				 |				img{border:none;}
				 |				li>p{display:inline;}
				 |			</style>
				 |		</head>
				 |	<body>
				 |	" + Комментарий + "
										|	</body>
										|</html>";

	Возврат Комментарий;
КонецФункции