#Область ОбработчикиСобытий

Процедура ПриСменеСтраницыПоляКомментария(Форма, Элемент, ТекущаяСтраница) Экспорт
	ПутьКДанным=РМ_MarkdownКлиентСервер.ПолучитьПутьКДаннымПоИменаПанелиСтраниц(Элемент.Имя);
	
	ОбновитьПолеПросмотраПоляКомментария(Форма, ПутьКДанным);
КонецПроцедуры

Процедура ОбработкаКомандыПоляКомментария(Форма, Команда) Экспорт
	СтруктураСтрокMD=Новый Структура;
	СтруктураСтрокMD.Вставить("ТекстMDНачалоВыделения", "");
	СтруктураСтрокMD.Вставить("ТекстMDКонцаВыделения", "");
	СтруктураСтрокMD.Вставить("ТекстMDНачалоСтроки", "");
	СтруктураСтрокMD.Вставить("ТекстMDСтрокаДо", "");
	СтруктураСтрокMD.Вставить("ТекстMDСтрокаПосле", "");
	СтруктураСтрокMD.Вставить("ТекстЗамены", "");

	Если СтрНайти(Команда.Имя, "РМ_КомандаMDЖирный") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "**";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "**";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЖирный_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDКурсив") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "*";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "*";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDКурсив_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDЗачеркнутый") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "~~";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = "~~";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЗачеркнутый_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDМаркерыСписок") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="* ";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDМаркерыСписок_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDНумерованныйСписок") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="1. ";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDНумерованныйСписок_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDЗаголовок1") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="# ";
		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЗаголовок1_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDЗаголовок2") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="## ";
		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЗаголовок2_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDЗаголовок3") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="### ";
		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЗаголовок3_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDЦитата") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="> ";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDЦитата_";
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDСписокЗадач") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоСтроки="* [ ] ";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDСписокЗадач_";

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDСсылка") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "[](";
		СтруктураСтрокMD.ТекстMDКонцаВыделения = ")";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDСсылка_";

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDПрограммныйКод") = 1 Тогда
		СтруктураСтрокMD.ТекстMDНачалоВыделения = "```" + Символы.ПС;
		СтруктураСтрокMD.ТекстMDКонцаВыделения = Символы.ПС + "```";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDПрограммныйКод_";

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDТаблица") = 1 Тогда
		СтруктураСтрокMD.ТекстMDСтрокаПосле = "
											  || Заголовок колонки 1 | Заголовок колонки 2 |
											  || ------ | ------ |
											  || Текст ячейки | Текст ячейки |
											  || Текст ячейки | Текст ячейки |";

		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDТаблица_";

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDВставитьИзображениеИзБуфераОбмена") = 1 Тогда
		СтруктураСтрокMD.ТекстЗамены= "РМ_КомандаMDВставитьИзображениеИзБуфераОбмена_";
		
		ПутьКДаннымБезЛишнего=СтрЗаменить(Команда.Имя, СтруктураСтрокMD.ТекстЗамены, "");
		#Если Не ВебКлиент Тогда
			ДопПараметры=Новый Структура;
			ДопПараметры.Вставить("Форма",Форма);
			ДопПараметры.Вставить("Команда",Команда);
			ДопПараметры.Вставить("ПутьКДаннымБезЛишнего",ПутьКДаннымБезЛишнего);
			ДопПараметры.Вставить("СтруктураСтрокMD",СтруктураСтрокMD);
			РМ_БуферОбменаКлиент.НачатьПолучениеКартинкиИзБуфера(Новый ОписаниеОповещения("КомандаMDВставитьИзображениеИзБуфераОбменаЗавершение",ЭтотОбъект,ДопПараметры),"ДвоичныеДанные");
			Возврат;	
		#КонецЕсли
	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDДобавитьФайл") = 1 Тогда

		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("ФормаВладелец", Форма);
		ПараметрыВыполнения.Вставить("ПутьКДаннымБезЛишнего", СтрЗаменить(Команда.Имя, "РМ_КомандаMDДобавитьФайл_", ""));
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		Обработчик = Новый ОписаниеОповещения("ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла", ЭтотОбъект,
			ПараметрыВыполнения);
		НачатьПомещениеФайла(Обработчик, , ДиалогВыбора, , Форма.УникальныйИдентификатор);

		Возврат;

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_ВставитьПрисоединенныйФайл") = 1 Тогда
		МассивНаименования=СтрРазделить(Команда.Имя, "_");
		ИдентификаторСтроки = Число(МассивНаименования[2]);

		МассивДляЗамены=Новый Массив;
		МассивДляЗамены.Добавить(МассивНаименования[0]);
		МассивДляЗамены.Добавить(МассивНаименования[1]);
		МассивДляЗамены.Добавить(МассивНаименования[2]);
		МассивДляЗамены.Добавить("");
		СтруктураСтрокMD.ТекстЗамены=СтрСоединить(МассивДляЗамены, "_");

		МассивНаименования.Удалить(0);
		МассивНаименования.Удалить(0);
		МассивНаименования.Удалить(0);

		ПутьКДаннымБезЛишнего=СтрСоединить(МассивНаименования, "_");
		ИмяТаблицыФайлов=РМ_MarkdownКлиентСервер.ИмяТаблицыПрисоединенныхФайлов(ПутьКДаннымБезЛишнего);

		СтрокаФайла = Форма[ИмяТаблицыФайлов].НайтиПоИдентификатору(ИдентификаторСтроки);
		ИмяФайла = СтрокаФайла.ИмяФайла;

		Если ЗначениеЗаполнено(ИмяФайла) Тогда
			СтруктураСтрокMD.ТекстMDНачалоВыделения = СтрШаблон("![](%1)", ИмяФайла);
		КонецЕсли;

	ИначеЕсли СтрНайти(Команда.Имя, "РМ_КомандаMDОткрытьСинтаксис") = 1 Тогда 
		ОткрытьФормуСинтаксиса();
		Возврат;
	Иначе
		Возврат;
	КонецЕсли;

	ВыполнитьЗаменуВТекстеКомментарияПоСтруктуреЗамены(Форма, Команда.Имя,СтруктураСтрокMD);
КонецПроцедуры

Процедура ПриНажатииПоляПросмотраКомментария(Форма, Элемент, ДанныеСобытия, СтандартнаяОбработка) Экспорт
	СтандартнаяОбработка=Ложь;

	Если СтрНачинаетсяС(ДанныеСобытия.Element.id, "Файл_") Тогда
		ИдентификаторФайла=СтрЗаменить(ДанныеСобытия.Element.id, "Файл_", "");
		СсылкаНаФайл=РМ_MarkdownСервер.ПолучитьСсылкуНаПрисоединенныйФайлПоИдентификатору(
			Новый УникальныйИдентификатор(ИдентификаторФайла));
		ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаФайл, Форма.УникальныйИдентификатор);
	ИначеЕсли ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		НачатьЗапускПриложения(ПустоеОписаниеОповещенияДляЗапускаПриложения(), ДанныеСобытия.href);
	КонецЕсли;

КонецПроцедуры

Процедура ОткрытьПрисоединенныйФайл(Форма, Команда) Экспорт
	МассивНаименования=СтрРазделить(Команда.Имя, "_");
	ИдентификаторСтроки = Число(МассивНаименования[2]);

	МассивНаименования.Удалить(0);
	МассивНаименования.Удалить(0);
	МассивНаименования.Удалить(0);

	ПутьКДаннымБезЛишнего= СтрСоединить(МассивНаименования, "_");

	ИмяТаблицыФайлов=РМ_MarkdownКлиентСервер.ИмяТаблицыПрисоединенныхФайлов(ПутьКДаннымБезЛишнего);
	СтрокаФайла = Форма[ИмяТаблицыФайлов].НайтиПоИдентификатору(ИдентификаторСтроки);
	НачатьЗапускПриложения(ПустоеОписаниеОповещенияДляЗапускаПриложения(), СтрокаФайла.ПутьКФайлу);

КонецПроцедуры

Процедура УдалитьНовыйПрисоединенныйФайл(Форма, Команда) Экспорт

	МассивНаименования=СтрРазделить(Команда.Имя, "_");
	ИдентификаторСтроки = Число(МассивНаименования[2]);

	МассивНаименования.Удалить(0);
	МассивНаименования.Удалить(0);
	МассивНаименования.Удалить(0);

	ПутьКДаннымБезЛишнего=СтрСоединить(МассивНаименования, "_");

	Элементы = Форма.Элементы;

	ИмяГруппыФайла=РМ_MarkdownКлиентСервер.ИмяГруппыНовогоПрисоединеногоФайла(ПутьКДаннымБезЛишнего,
		ИдентификаторСтроки);
	ГруппаПрисоединенногоФайла = Элементы.Найти(ИмяГруппыФайла);
	Если ГруппаПрисоединенногоФайла <> Неопределено Тогда
		ГруппаПрисоединенногоФайла.Видимость = Ложь;
	КонецЕсли;

	ИмяТаблицыПрисоединенныхФайлов=РМ_MarkdownКлиентСервер.ИмяТаблицыПрисоединенныхФайлов(ПутьКДаннымБезЛишнего);
	СтрокаФайла = Форма[ИмяТаблицыПрисоединенныхФайлов].НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаФайла.ПутьКФайлу = "";
	СтрокаФайла.ИмяФайла = "";
	СтрокаФайла.Адрес = "";
	
	//Делаем невидимым кнопку вставить
	ИмяКомандыВставить=РМ_MarkdownКлиентСервер.ИмяКомандыВставитьНовогоПрисоединеногоФайла(ПутьКДаннымБезЛишнего,
		ИдентификаторСтроки);
	КнопкаВставить=Элементы.Найти(ИмяКомандыВставить);
	Если КнопкаВставить <> Неопределено Тогда
		КнопкаВставить.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

Процедура ДокументСформированПоляПросмотраКомментария(Форма, Элемент) Экспорт
	ПутьКДанным=РМ_MarkdownКлиентСервер.ПолучитьПутьКДаннымПоИмениПоляПросмотра(Элемент.Имя);
	
	ОбновитьПолеПросмотраПоляКомментария(Форма, ПутьКДанным);
КонецПроцедуры


#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ИзменитьВариантПодсветкиСинтаксиса(Форма, ПутьКДанным, ВариантПодсветки) Экспорт
	ПутьКПолюПросмотра=РМ_MarkdownКлиентСервер.ПолучитьИмяЭлементаПросмотраКомментария(ПутьКДанным);
	Форма[ПутьКПолюПросмотра]=РМ_MarkdownСервер.СформироватьТекстHTMLДляТекстаMarkdown(ВариантПодсветки);
		
КонецПроцедуры

Процедура ОбновитьПолеПросмотраПоляКомментария(Форма, ПутьКДанным) Экспорт
	ПутьКДаннымБезЛишнего=РМ_MarkdownКлиентСервер.ПолучитьПутьКДаннымБезЛишнего(ПутьКДанным);
	ТекущаяСтраница=ТекущаяСтраницаПанелиРедактированияКомментария(Форма, ПутьКДанным);
	
	Если ТекущаяСтраница = Форма.Элементы[РМ_MarkdownКлиентСервер.ПолучитьИмяСтраницыПросмотраКомментария(ПутьКДанным)] Тогда

		ТекстДляПросмотра = ТекстКомментария(Форма, ПутьКДанным);

		ДанныеПрисоединенныхФайлов = РМ_MarkdownКлиентСервер.ДанныеПрисоединенныхФайлов(Форма, ПутьКДаннымБезЛишнего);

		РМ_MarkdownКлиентСервер.СконвертироватьИменаПрисоединенныхФайловВТексте(ДанныеПрисоединенныхФайлов,
			ТекстДляПросмотра);
		
		ЭлементПросмотраКомментария=Форма.Элементы[РМ_MarkdownКлиентСервер.ПолучитьИмяЭлементаПросмотраКомментария(ПутьКДанным)];
		
		ОчиститьТекстыПоляHTML(ЭлементПросмотраКомментария);
		ДобавитьТекстВТекстыПоляHTML(ЭлементПросмотраКомментария,"preview",ТекстДляПросмотра);
		СконвертироватьТекстыВHTML(ЭлементПросмотраКомментария);
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьТекстВТекстыПоляHTML(Элемент, Ключ, Текст) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	Документ.addText(Ключ, Текст);
КонецПроцедуры

Процедура УдалитьТекстПоляКомментария(Элемент, Ключ) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	Документ.deleteText(Ключ);
	
КонецПроцедуры

Процедура ОчиститьТекстыПоляHTML(Элемент) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	Документ.clearTexts();
КонецПроцедуры

Процедура СконвертироватьТекстыВHTML(Элемент) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	Документ.markdownConvert();
КонецПроцедуры

Процедура ДобавитьНовыйПрисоединенныйФайл(ИмяФайла, ПутьКФайлу, Форма, ПутьКДаннымБезЛишнего, Адрес = Неопределено) Экспорт

	Если Адрес = Неопределено Тогда
		Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПутьКФайлу), Форма.УникальныйИдентификатор);
	КонецЕсли;

	ИмяТаблицыФайлов=РМ_MarkdownКлиентСервер.ИмяТаблицыПрисоединенныхФайлов(ПутьКДаннымБезЛишнего);

	СтрокаПрисоединенногоФайла = Форма[ИмяТаблицыФайлов].Добавить();
	СтрокаПрисоединенногоФайла.ПутьКФайлу = ПутьКФайлу;
	СтрокаПрисоединенногоФайла.ИмяФайла = ИмяФайла;
	СтрокаПрисоединенногоФайла.Адрес = Адрес;
	СтрокаПрисоединенногоФайла.ЭтоНовый = Истина;

	Форма.РМ_Подключаемый_ПриДобавленииПрисоединенногоФайла(ИмяФайла, СтрокаПрисоединенногоФайла.ПолучитьИдентификатор(),
		ПутьКДаннымБезЛишнего);

КонецПроцедуры

Функция ТекстHTMLПоТекстуMarkdown(Элемент, ТекстMarkdown) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	
	Возврат Документ.mdToHtml(ТекстMarkdown);
КонецФункции

Функция ТекстMarkdownПоТекстуHTML(Элемент, ТекстHTML) Экспорт
	Документ=ДокументHTMLДляЭлементаHTML(Элемент);
	Возврат Документ.htmlToMd(ТекстHTML);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедуры

Процедура ВыполнитьЗаменуВТекстеКомментарияПоСтруктуреЗамены(Форма, ИмяКоманды, СтруктураСтрокMD)
	ИмяЭлементаФормы=РМ_MarkdownКлиентСервер.ПрефиксЭлементовФормы() + Сред(ИмяКоманды, СтрДлина(СтруктураСтрокMD.ТекстЗамены) + 1);

	ГраницыВыделения=Новый Структура("НачалоСтроки,КонецСтроки,НачалоКолонки,КонецКолонки");
	Форма.Элементы[ИмяЭлементаФормы].ПолучитьГраницыВыделения(ГраницыВыделения.НачалоСтроки,
		ГраницыВыделения.НачалоКолонки, ГраницыВыделения.КонецСтроки, ГраницыВыделения.КонецКолонки);
	ПутьКДанным=СтрЗаменить(ИмяЭлементаФормы, РМ_MarkdownКлиентСервер.ПрефиксЭлементовФормы(), "");
	ПутьКДанным=СтрЗаменить(ПутьКДанным, "_", ".");

	ТекстКомментария=ТекстКомментария(Форма, ПутьКДанным);

	КомандаMDДобавитьШаблон(ТекстКомментария, ГраницыВыделения, СтруктураСтрокMD);

	УстановитьТекстКомментария(Форма, ПутьКДанным, ТекстКомментария);
КонецПроцедуры

Процедура КомандаMDВставитьИзображениеИзБуфераОбменаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) <> Тип("ДвоичныеДанные") Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКДаннымБезЛишнего=ДополнительныеПараметры.ПутьКДаннымБезЛишнего;
	СтруктураСтрокMD=ДополнительныеПараметры.СтруктураСтрокMD;
	//ДопПараметры.Вставить("Форма",Форма);
	//		ДопПараметры.Вставить("Команда",Команда);
	//		ДопПараметры.Вставить("ПутьКДаннымБезЛишнего",ПутьКДаннымБезЛишнего);

	
	ПутьКФайлу=ПолучитьИмяВременногоФайла("png");
	Результат.Записать(ПутьКФайлу);
	
	ИмяФайла = РМ_MarkdownКлиентСервер.РазложитьПолноеИмяФайла(ПутьКФайлу).Имя;
	СтруктураСтрокMD.ТекстMDНачалоВыделения = СтрШаблон("![](%1)", ИмяФайла);
	ИмяТаблицыФайлов=РМ_MarkdownКлиентСервер.ИмяТаблицыПрисоединенныхФайлов(ПутьКДаннымБезЛишнего);
	Если ДополнительныеПараметры.Форма[ИмяТаблицыФайлов].НайтиСтроки(
		Новый Структура("ИмяФайла", ИмяФайла)).Количество() = 0 Тогда
		
		ДобавитьНовыйПрисоединенныйФайл(ИмяФайла, ПутьКФайлу, ДополнительныеПараметры.Форма, ПутьКДаннымБезЛишнего);
	Иначе
		Сообщить("Файл с таким именем уже добавлен");
	КонецЕсли;
	ВыполнитьЗаменуВТекстеКомментарияПоСтруктуреЗамены(ДополнительныеПараметры.Форма,ДополнительныеПараметры.Команда.Имя,СтруктураСтрокMD);
КонецПроцедуры

Процедура ДобавитьИзФайловойСистемыБезРасширенияПослеЗагрузкиФайла(Помещен, Адрес, ВыбранноеИмяФайла,
	ПараметрыВыполнения) Экспорт

	Если Не Помещен Тогда
		Возврат;
	КонецЕсли;

	СтруктураПути = РМ_MarkdownКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
	ДобавитьНовыйПрисоединенныйФайл(СтруктураПути.Имя, ВыбранноеИмяФайла, ПараметрыВыполнения.ФормаВладелец,
		ПараметрыВыполнения.ПутьКДаннымБезЛишнего, Адрес);

КонецПроцедуры

Процедура СдвинутьГраницыВыделения(ГраницыВыделения, ТекстВставки, СтрокаКонцаДо)
		//двигаем границы выделения
	СтрокиТекста=СтрРазделить(ТекстВставки, Символы.ПС);
	КоличествоСтрокТекста=СтрокиТекста.Количество();

	ГраницыВыделения.КонецСтроки=ГраницыВыделения.КонецСтроки + КоличествоСтрокТекста - 1;
	Если КоличествоСтрокТекста = 1 И ГраницыВыделения.НачалоСтроки = ГраницыВыделения.КонецСтроки Тогда
		ГраницыВыделения.КонецКолонки=ГраницыВыделения.КонецКолонки + СтрДлина(ТекстВставки);
	ИначеЕсли КоличествоСтрокТекста > 1 Тогда
		ГраницыВыделения.КонецКолонки=СтрДлина(СтрокиТекста[СтрокиТекста.Количество() - 1]) + СтрДлина(СтрокаКонцаДо)
			+ 1;
	КонецЕсли;

	Если ГраницыВыделения.КонецКолонки = 0 Тогда
		ГраницыВыделения.КонецКолонки=1;
	КонецЕсли;

КонецПроцедуры

Процедура КомандаMDДобавитьШаблон(ТекстКомментария, ГраницыВыделения, СтруктураСтрокMD)

	ТекстовыйДокумент=Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстКомментария);

	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDНачалоВыделения) Тогда
		
		//В начало вставляем данную строку
		СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.НачалоСтроки);

		СтрокаВставки=Лев(СтрокаВставки, ГраницыВыделения.НачалоКолонки - 1) + СтруктураСтрокMD.ТекстMDНачалоВыделения
			+ Сред(СтрокаВставки, ГраницыВыделения.НачалоКолонки);

		СтрокаКонцаВыделения=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.КонецСтроки);

		ТекстовыйДокумент.УдалитьСтроку(ГраницыВыделения.НачалоСтроки);
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки, СтрокаВставки);

		СдвинутьГраницыВыделения(ГраницыВыделения, СтруктураСтрокMD.ТекстMDНачалоВыделения, СтрокаКонцаВыделения);
	КонецЕсли;

	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDКонцаВыделения) Тогда
		//В начало вставляем данную строку
		СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.КонецСтроки);

		СтрокаВставки=Лев(СтрокаВставки, ГраницыВыделения.КонецКолонки - 1) + СтруктураСтрокMD.ТекстMDКонцаВыделения
			+ Сред(СтрокаВставки, ГраницыВыделения.КонецКолонки);

		СтрокаКонцаВыделения=ТекстовыйДокумент.ПолучитьСтроку(ГраницыВыделения.КонецСтроки);

		ТекстовыйДокумент.УдалитьСтроку(ГраницыВыделения.КонецСтроки);
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.КонецСтроки, СтрокаВставки);

		СдвинутьГраницыВыделения(ГраницыВыделения, СтруктураСтрокMD.ТекстMDКонцаВыделения, СтрокаКонцаВыделения);

	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDНачалоСтроки) Тогда
		Для НомерСтроки = ГраницыВыделения.НачалоСтроки По ГраницыВыделения.КонецСтроки Цикл
			СтрокаВставки=ТекстовыйДокумент.ПолучитьСтроку(НомерСтроки);
			СтрокаВставки=СтруктураСтрокMD.ТекстMDНачалоСтроки + СтрокаВставки;

			ТекстовыйДокумент.УдалитьСтроку(НомерСтроки);
			ТекстовыйДокумент.ВставитьСтроку(НомерСтроки, СтрокаВставки);
		КонецЦикла;
	КонецЕсли;

	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDСтрокаДо) Тогда
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки, СтруктураСтрокMD.ТекстMDСтрокаДо);
	КонецЕсли;

	Если ЗначениеЗаполнено(СтруктураСтрокMD.ТекстMDСтрокаПосле) Тогда
		ТекстовыйДокумент.ВставитьСтроку(ГраницыВыделения.НачалоСтроки + ?(ЗначениеЗаполнено(
			СтруктураСтрокMD.ТекстMDСтрокаДо), 1, 0), СтруктураСтрокMD.ТекстMDСтрокаПосле);
	КонецЕсли;
	ТекстКомментария=ТекстовыйДокумент.ПолучитьТекст();
КонецПроцедуры

Процедура MDДобавитьПереводСтроки(ТекстКомментария)
	Если ЗначениеЗаполнено(ТекстКомментария) Тогда
		ТекстКомментария = ТекстКомментария + Символы.ПС + Символы.ПС;
	КонецЕсли;
КонецПроцедуры

Функция ПустоеОписаниеОповещенияДляЗапускаПриложения() Экспорт
	Возврат Новый ОписаниеОповещения("НачатьЗапускПриложенияЗавершениеПустое", ЭтотОбъект);
КонецФункции

Процедура НачатьЗапускПриложенияЗавершениеПустое(КодВозврата, ДополнительныеПараметры) Экспорт
	Если КодВозврата = Неопределено Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры



Процедура ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаПрисоединенныйФайл, УникальныйИдентификатор) Экспорт
	РМ_MarkdownКлиентПереопределяемый.ОткрытьПрисоединенныйФайлПоСсылке(СсылкаНаПрисоединенныйФайл,УникальныйИдентификатор);
КонецПроцедуры

Функция ДокументHTMLДляЭлементаHTML(Элемент) 
	Возврат Элемент.Документ.defaultView;
КонецФункции

Функция ТекстКомментария(Форма, ПутьКДанным)
	МассивПути=СтрРазделить(ПутьКДанным, ".", Ложь);
	Если МассивПути.Количество() = 1 Тогда
		ТекстКомментария=Форма[МассивПути[0]];
	ИначеЕсли МассивПути.Количество() = 2 Тогда
		ТекстКомментария=Форма[МассивПути[0]][МассивПути[1]];
	Иначе
		ТекстКомментария="";
	КонецЕсли;
	
	Возврат ТекстКомментария;
КонецФункции

Процедура УстановитьТекстКомментария(Форма, ПутьКДанным, ТекстКомментария)
	МассивПути=СтрРазделить(ПутьКДанным, ".", Ложь);
	Если МассивПути.Количество() = 1 Тогда
		Форма[МассивПути[0]]=ТекстКомментария;
	ИначеЕсли МассивПути.Количество() = 2 Тогда
		Форма[МассивПути[0]][МассивПути[1]]=ТекстКомментария;
	Иначе
		
	КонецЕсли;	
КонецПроцедуры

Функция ТекущаяСтраницаПанелиРедактированияКомментария(Форма, ПутьКДанным)
	Возврат Форма.Элементы[РМ_MarkdownКлиентСервер.ПолучитьИмяЭлементаПанелиСтраницРедактированияКомментария(ПутьКДанным)].ТекущаяСтраница;
КонецФункции

Процедура ОткрытьФормуСинтаксиса() Экспорт
	ОткрытьФорму("ОбщаяФорма.РМ_MarkdownСинтаксис");
КонецПроцедуры

#КонецОбласти