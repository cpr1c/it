#Область ПрограммныйИнтерфейс

Процедура ПриСозданииНаСервере(Форма, ГруппаЭлементовТекста, ПутьКДанным, ФорматТекста = Неопределено) Экспорт
	ФорматТекстаРедактирования=ФорматТекстаРедактирования(ФорматТекста);

	Если ФорматТекстаРедактирования = Перечисления.ВариантыОформленияТекста.Markdown Тогда
		РМ_MarkdownСервер.ВставитьПолеРедактированияТекстаНаФорму(Форма, ГруппаЭлементовТекста, ПутьКДанным);
	Иначе
		РВ_РедакторWysiwygHTML.ВставитьПолеРедактированияТекстаНаФорму(Форма, ГруппаЭлементовТекста, ПутьКДанным);
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписьюНаСервере(Форма, ТекущийОбъект, ПутьКДанным, ФорматТекста = Неопределено) Экспорт

	ТекущийОбъект.ДополнительныеСвойства.Вставить("ДанныеПрисоединенныхФайлов",
		РедакторКомментарияКлиентСервер.ДанныеПрисоединенныхФайлов(Форма, ПутьКДанным, ФорматТекста));

КонецПроцедуры

Процедура ПриЗаписиНаСервере(Форма, ВладелецФайлов, ПутьКДанным, ФорматТекста = Неопределено, Предмет = Неопределено) Экспорт
	ФорматТекстаРедактирования=ФорматТекстаРедактирования(ФорматТекста);

	Если ФорматТекстаРедактирования = Перечисления.ВариантыОформленияТекста.Markdown Тогда
		РМ_MarkdownСервер.ЗаписатьНовыеПрисоединенныеФайлы(Форма, ВладелецФайлов, ПутьКДанным, Предмет);
	Иначе
		РВ_РедакторWysiwygHTML.ЗаписатьНовыеПрисоединенныеФайлы(Форма, ВладелецФайлов, ПутьКДанным, Предмет);
	КонецЕсли;

КонецПроцедуры

Процедура ПослеЗаписиНаСервере(Форма, ПутьКДанным, ФорматТекста = Неопределено) Экспорт
	ФорматТекстаРедактирования=ФорматТекстаРедактирования(ФорматТекста);

	Если ФорматТекстаРедактирования = Перечисления.ВариантыОформленияТекста.Markdown Тогда
		РМ_MarkdownСервер.УдалитьНовыеПрисоединенныеФайлыСФормыПослеЗаписи(Форма, ПутьКДанным);
	Иначе
		РВ_РедакторWysiwygHTML.УдалитьНовыеПрисоединенныеФайлыСФормыПослеЗаписи(Форма, ПутьКДанным);
	КонецЕсли;

КонецПроцедуры

Процедура ПрочитатьПрисоединенныеФайлы(Форма, ВладелецФайлов, ПутьКДанным, ФорматТекста = Неопределено) Экспорт
	ФорматТекстаРедактирования=ФорматТекстаРедактирования(ФорматТекста);

	Если ФорматТекстаРедактирования = Перечисления.ВариантыОформленияТекста.Markdown Тогда
		РМ_MarkdownСервер.ПрочитатьПрисоединенныеФайлы(Форма, ВладелецФайлов, ПутьКДанным);
	Иначе
		РВ_РедакторWysiwygHTML.ПрочитатьПрисоединенныеФайлы(Форма, ВладелецФайлов, ПутьКДанным);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ФорматТекстаРедактирования(ФорматТекста = Неопределено) Экспорт
	Если ФорматТекста = Неопределено Тогда
		ФорматТекстаРедактирования=Константы.ВариантОформленияТекста.Получить();
	Иначе
		ФорматТекстаРедактирования=ФорматТекста;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(ФорматТекстаРедактирования) Тогда
		ФорматТекстаРедактирования=Перечисления.ВариантыОформленияТекста.HTML;
	КонецЕсли;

	Возврат ФорматТекстаРедактирования;
КонецФункции

Функция ВариантыОформленияТекстаКомментария() Экспорт
	Варианты=Новый Структура;

	Для Каждого ТекЗначение Из Метаданные.Перечисления.ВариантыОформленияТекста.ЗначенияПеречисления Цикл
		Варианты.Вставить(ТекЗначение.Имя, Перечисления.ВариантыОформленияТекста[ТекЗначение.Имя]);
	КонецЦикла;

	Возврат Варианты;
КонецФункции

#КонецОбласти