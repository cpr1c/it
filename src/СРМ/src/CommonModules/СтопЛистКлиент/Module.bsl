#Область ПрограммныйИнтерфейс

Процедура ДобавитьПартнераВСтопЛист(Партнер) Экспорт
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Партнер", Партнер);
	ПоказатьВводСтроки(Новый ОписаниеОповещения("ДобавитьПартнераВСтопЛистЗавершениеВводаПричины", ЭтотОбъект,
		ДопПараметры), "", "Введите причину отправки в стоп лист", 200, Истина);
КонецПроцедуры

Процедура УдалитьПартнераИзСтопЛиста(Партнер) Экспорт
	СтопЛист.УдалитьПартнераИзСтопЛиста(Партнер);
	ОповеститьОбИзмененииПринадлежностиКСтопЛисту(Партнер);
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьПартнераВСтопЛистЗавершениеВводаПричины(Строка, ДополнительныеПараметры) Экспорт
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтопЛист.ДобавитьПартнераВСтопЛист(ДополнительныеПараметры.Партнер, Строка);

	ОповеститьОбИзмененииПринадлежностиКСтопЛисту(ДополнительныеПараметры.Партнер);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОповеститьОбИзмененииПринадлежностиКСтопЛисту(Партнер) Экспорт
	Оповестить("ИзмененаПринадлежностьКСтопЛисту", Партнер);
КонецПроцедуры

#КонецОбласти