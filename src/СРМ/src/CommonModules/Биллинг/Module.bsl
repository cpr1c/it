// Вызывается из события ОбработкаЗаполнения модулей объектов демонстрационных документов.
//
// Параметры:
//  Объект - СправочникОбъект, ДокументОбъект - заполняемый объект;
//  ИмяРеквизита - Строка - имя реквизита Организация.
//
Процедура ПриВводеНовогоЗаполнитьОрганизацию(Объект, ИмяРеквизита = "Организация") Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Организации.Ссылка КАК Организация
	|ИЗ
	|	Справочник.Организации КАК Организации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Объект[ИмяРеквизита] = Выборка.Организация;
	КонецЕсли;
КонецПроцедуры

// Возвращает ссылку на первый найденный документ заключения или изменения договора
// где номер совпадает с текущим, а ссылка на справочник договоры другая
//
Функция ПроверитьУникальностьНомераДоговора(НомерДоговора, ИсключаемыйДоговор = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДоговора.Регистратор
	|ИЗ
	|	РегистрСведений.ДанныеДоговора КАК ДанныеДоговора
	|ГДЕ
	|	ДанныеДоговора.НомерДоговора = &НомерДоговора
	|	И ДанныеДоговора.Договор <> &ИсключаемыйДоговор";
	Запрос.УстановитьПараметр("НомерДоговора", НомерДоговора);
	Запрос.УстановитьПараметр("ИсключаемыйДоговор", ИсключаемыйДоговор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Регистратор;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

