Процедура ПриНачалеРаботыСистемы() Экспорт
	ПланГлобальногоПоиска=Новый ПланГлобальногоПоиска;
	
	//Поиск по номеру задачи ИмяПроцедуры, Модуль, НаСервере, Фоновый, Порядок, ДополнительныеПараметры
	ЭлементПОиска=Новый ЭлементПланаГлобальногоПоиска("ПоискЗадачиПоНомеру", "ГлобальныйПоискСервер", Истина, Истина, 0);
	ПланГлобальногоПоиска.Добавить(ЭлементПОиска);

	ЭлементПОиска=Новый ЭлементПланаГлобальногоПоиска("ПоискПроектовПоПодстрокеНаименования", "ГлобальныйПоискСервер",
		Истина, Истина, 1);
	ПланГлобальногоПоиска.Добавить(ЭлементПОиска);

	ЭлементПОиска=Новый ЭлементПланаГлобальногоПоиска("ДобавитьРезультатПоискПолнотекстовый", "ГлобальныйПоискСервер",
		Истина, Истина, 1);
	ПланГлобальногоПоиска.Добавить(ЭлементПОиска);

	ЭлементПОиска=Новый ЭлементПланаГлобальногоПоиска(СтандартныйВидГлобальногоПоиска.НавигационнаяСсылка, 2);
	ПланГлобальногоПоиска.Добавить(ЭлементПОиска);

	ГлобальныйПоиск.УстановитьПлан(ПланГлобальногоПоиска);
	ГлобальныйПоиск.УстановитьОписание("Если введен номер задачи, откроектся задача.");
	ГлобальныйПоиск.УстановитьПодсказкуВвода("Номер задачи или часть имени проекта");
КонецПроцедуры

Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка) Экспорт
	Если ЭлементРезультата.ВидПоиска = "ПоискПроектовПоПодстрокеНаименования" Тогда
		СтандартнаяОбработка=Ложь;

		Оповестить("НайденПроектВГлобальномПоиске", ЭлементРезультата.Значение);
	ИначеЕсли ЭлементРезультата.ВидПоиска = "ДобавитьРезультатПоискПолнотекстовый" Тогда
		СтандартнаяОбработка=Ложь;
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПереданнаяСтрокаПоиска", ЭлементРезультата.Описание);

		ОткрытьФорму("ОбщаяФорма.ФормаПоиска", ПараметрыФормы, , Истина);

	КонецЕсли;
КонецПроцедуры